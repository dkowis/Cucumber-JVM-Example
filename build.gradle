apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: 'idea'

//build stuff
sourceCompatibility = 1.6
targetCompatibility = 1.6

configurations {
    cucumberRuntime {
        extendsFrom testRuntime
    }
}

task cucumber() {
    dependsOn assemble, testClasses
    doLast {
        javaexec {
            main = "cucumber.cli.Main"
            classpath = configurations.cucumberRuntime
            args = ['-f', 'pretty', '--glue', 'com.rackspace.example.stack.step_definitions', 'src/test/resources']

        }
    }
}

dependencies {
    // Groovy library for groovy building!
    groovy 'org.codehaus.groovy:groovy-all:2.0.0-beta-2'

    /*
   In order to work around a really flagrant bug ( http://issues.gradle.org/browse/GRADLE-732 )
   You have to create a configuration that includes the jar.archivePath (the jar.archivePath is created by the java plugin)
   and give it the same name as the cucumberRuntime configuration, or name it something different and have the cucumberRuntime
   configuration extend from it as well.

   VERY ANNOYING BUG
    */
    cucumberRuntime files("${jar.archivePath}")

    testCompile 'junit:junit:4.10'
    testCompile 'info.cukes:cucumber-junit:1.0.0.RC15'
    testCompile 'info.cukes:cucumber-spring:1.0.0.RC15'

    testCompile 'org.springframework:spring-core:3.0.6.RELEASE'
    testCompile 'org.springframework:spring-beans:3.0.6.RELEASE'
    testCompile 'org.springframework:spring-context:3.0.6.RELEASE'
}

repositories {
    mavenCentral()
}
